PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gaf: <http://groundedannotationframework.org/gaf#>
PREFIX grasp: <http://groundedannotationframework.org/grasp#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX graspf: <http://groundedannotationframework.org/grasp/factuality#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX sem: <http://semanticweb.cs.vu.nl/2009/11/sem/>


select ?val ?authorlabel ?date (GROUP_CONCAT(DISTINCT ?p ;separator="|") as ?predicates)
where {
    BIND(<%s> AS ?p) .
    BIND(<%s> AS ?s) .
    BIND(<%s> AS ?o) .

    # Takes too long to query subproperties
    # ?s ?p ?o .
    # ?pOriginal rdfs:subPropertyOf* ?p .
    # GRAPH ?g {
    #     ?s ?pOriginal ?o .
    # } .

    ?s ?p ?o .

    GRAPH ?g {
        ?s ?p ?o .
    } .

    ?g gaf:denotedBy ?m .
    ?m grasp:hasAttribution / rdf:value ?val .
    ?val rdf:type graspf:PolarityValue .

    ?m prov:wasDerivedFrom ?utt .

    ?cont sem:hasEvent / sem:hasSubEvent ?utt .
    ?cont ?time_pred ?d .
    ?d rdfs:label ?date .
    VALUES (?time_pred) { (sem:hasTime) (sem:hasBeginTimeStamp) } .

    ?m grasp:wasAttributedTo ?author .
    OPTIONAL {
        ?author rdfs:label ?authorlabel .
    }
}
GROUP BY ?val ?authorlabel ?date